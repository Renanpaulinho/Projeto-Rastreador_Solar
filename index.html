<!DOCTYPE html>
<html lang="pt-br">
<head>
  <meta charset="UTF-8" />
  <meta name="viewport" content="width=device-width, initial-scale=1.0"/>
  <title>Código Arduino R4 – Rastreador Solar</title>

  <!-- Prism.js e tema escuro -->
  <link href="https://cdn.jsdelivr.net/npm/prismjs/themes/prism-tomorrow.css" rel="stylesheet" />
  <script src="https://cdn.jsdelivr.net/npm/prismjs/prism.js"></script>
  <script src="https://cdn.jsdelivr.net/npm/prismjs/components/prism-clike.min.js"></script>
  <script src="https://cdn.jsdelivr.net/npm/prismjs/components/prism-cpp.min.js"></script>

  <style>
    body {
      margin: 0;
      padding: 20px;
      background-color: #1e1e1e;
      color: #d4d4d4;
      font-family: 'Fira Code', monospace;
    }
    h2 {
      font-family: 'Segoe UI', sans-serif;
      color: #ffffff;
      margin-bottom: 20px;
    }
    pre {
      background-color: #1e1e1e;
      color: #d4d4d4;
      padding: 20px;
      border-radius: 10px;
      overflow: auto;
      box-shadow: 0 0 10px rgba(0,0,0,0.4);
      font-size: 14px;
      line-height: 1.5;
    }
    .token.comment,
    .token.prolog,
    .token.doctype,
    .token.cdata {
      color: #6A9955; 
      font-style: italic;
    }
    @media (max-width: 600px) {
      body { padding: 10px; }
      pre { font-size: 13px; }
    }
  </style>
</head>
<body>

  <h2>Código Arduino R4: Rastreamento Solar + ThingSpeak</h2>
  <pre><code class="language-cpp" id="codigo"></code></pre>

  <script>
    const codigoArduino =  `#include <WiFi.h>
#include <ArduinoHttpClient.h>
#include "Arduino_LED_Matrix.h"
#include <Servo.h>
#include <EEPROM.h>

ArduinoLEDMatrix matrix;
Servo servoAzimuth;
Servo servoElevation;

const int PINO_SERVO_AZIMUTH = 8;
const int PINO_SERVO_ELEVATION = 9;
const int PINO_ADC_VOLTAGEM = A0;
const int PINO_BOTAO_MODO = 4;
const int PINO_ADC_CORRENTE = A5;

const int LDR_TOP_LEFT = A1;
const int LDR_TOP_RIGHT = A2;
const int LDR_BOTTOM_LEFT = A3;
const int LDR_BOTTOM_RIGHT = A4;

//Insira aqui os dados da sua rede Wi-Fi 
const char* ssid = "";      // Nome da rede Wi-Fi
const char* password = "";  // Senha da rede Wi-Fi

//Insira aqui sua chave de escrita do ThingSpeak
const char* apiKey = "";    // Chave API (Write API Key)

const long INTERVALO_LEITURAS = 2000;
const long INTERVALO_MOVIMENTO_SERVO = 10000;
const long INTERVALO_ENVIO_DADOS = 15000;
const long INTERVALO_RECONEXAO = 60000;

unsigned long tempoAnteriorLeituras = 0;
unsigned long tempoAnteriorMovimento = 0;
unsigned long tempoAnteriorEnvio = 0;
unsigned long tempoAnteriorReconexao = 0;

bool modoLDR = false;
int horaSimulada = 6;
int anguloElevacao = 90;
int anguloAzimute = 90;
float voltagemAtual = 0.0;
float correnteAtual = 0.0;

int ultimoEstadoBotao = HIGH;
unsigned long ultimoTempoDebounce = 0;
const long delayDebounce = 50;

#define EEPROM_TAMANHO 512
bool modoOffline = true;
int enderecoEEPROM = 0;
struct DadoSolar { int azimute; int elevacao; float voltagem; float corrente; };
const int BYTES_POR_REGISTRO = sizeof(DadoSolar);

void animacaoConectando() {
  matrix.loadSequence(LEDMATRIX_ANIMATION_WIFI_SEARCH);
  matrix.play(true);
}
void animacaoConectado() {
  matrix.loadFrame(LEDMATRIX_HEART_BIG);
}
void animacaoErro() {
  matrix.loadFrame(LEDMATRIX_DANGER);
}

void iniciarConexaoWiFi() {
  Serial.println("Tentando conectar ao Wi-Fi...");
  animacaoConectando();
  WiFi.begin(ssid, password);
}

void salvarNaEEPROM(const DadoSolar& dados) {
  if (enderecoEEPROM + BYTES_POR_REGISTRO > EEPROM_TAMANHO) return;
  EEPROM.put(enderecoEEPROM, dados);
  enderecoEEPROM += BYTES_POR_REGISTRO;
}

void enviarDadosThingSpeak(int azimute, int elevacao, float voltagem, float corrente) {
  if (WiFi.status() != WL_CONNECTED) {
    modoOffline = true;
    animacaoErro();
    return;
  }
  WiFiClient client;
  HttpClient http(client, "api.thingspeak.com", 80);
  http.setHttpResponseTimeout(10000);
  String url = "/update?api_key=" + String(apiKey) +
               "&field1=" + String(azimute) +
               "&field2=" + String(elevacao) +
               "&field3=" + String(voltagem) +
               "&field4=" + String(corrente);
  http.get(url);
  int httpResponseCode = http.responseStatusCode();
  if (httpResponseCode == 200) {
    animacaoConectado();
  } else {
    animacaoErro();
    modoOffline = true;
  }
}

void enviarDadosEEPROM() {
  animacaoConectado();
  for (int addr = 0; addr < enderecoEEPROM; addr += BYTES_POR_REGISTRO) {
    if (WiFi.status() != WL_CONNECTED) {
      modoOffline = true;
      animacaoErro();
      return;
    }
    DadoSolar dadosLidos;
    EEPROM.get(addr, dadosLidos);
    enviarDadosThingSpeak(dadosLidos.azimute, dadosLidos.elevacao, dadosLidos.voltagem, dadosLidos.corrente);
    delay(1000);
  }
  for (int i = 0; i < enderecoEEPROM; i++) EEPROM.write(i, 0);
  enderecoEEPROM = 0;
  modoOffline = false;
}

void lerSensores() {
  int leituraAdcVoltagem = analogRead(PINO_ADC_VOLTAGEM);
  voltagemAtual = (leituraAdcVoltagem / 1023.0) * 25.0;
  int leituraAdcCorrente = analogRead(PINO_ADC_CORRENTE);
  float tensaoSensor = (leituraAdcCorrente / 1023.0) * 5.0;
  correnteAtual = tensaoSensor;
}

void moverServosSimulado() {
  horaSimulada++;
  if (horaSimulada >= 24) horaSimulada = 0;
  if (horaSimulada >= 6 && horaSimulada <= 18) {
    anguloAzimute = map(horaSimulada, 6, 18, 0, 180);
    float anguloNormalizado = abs(horaSimulada - 12);
    anguloElevacao = 90 - (anguloNormalizado * 15);
  } else {
    anguloElevacao = 0;
  }
  servoAzimuth.write(anguloAzimute);
  servoElevation.write(anguloElevacao);
}

void moverServosComLDR() {
  int topLeft = analogRead(LDR_TOP_LEFT);
  int topRight = analogRead(LDR_TOP_RIGHT);
  int bottomLeft = analogRead(LDR_BOTTOM_LEFT);
  int bottomRight = analogRead(LDR_BOTTOM_RIGHT);
  int diffAzimute = (topLeft + bottomLeft) - (topRight + bottomRight);
  int diffElevacao = (topLeft + topRight) - (bottomLeft + bottomRight);
  int tolerancia = 50;
  if (abs(diffAzimute) > tolerancia) anguloAzimute += (diffAzimute > 0) ? -1 : 1;
  if (abs(diffElevacao) > tolerancia) anguloElevacao += (diffElevacao > 0) ? 1 : -1;
  anguloAzimute = constrain(anguloAzimute, 0, 180);
  anguloElevacao = constrain(anguloElevacao, 0, 180);
  servoAzimuth.write(anguloAzimute);
  servoElevation.write(anguloElevacao);
}

void verificarBotaoDeModo() {
  int leitura = digitalRead(PINO_BOTAO_MODO);
  if (leitura == LOW && ultimoEstadoBotao == HIGH) {
    if (millis() - ultimoTempoDebounce > delayDebounce) {
      modoLDR = !modoLDR;
      ultimoTempoDebounce = millis();
    }
  }
  ultimoEstadoBotao = leitura;
}

void setup() {
  Serial.begin(9600);
  matrix.begin();
  pinMode(PINO_BOTAO_MODO, INPUT_PULLUP);
  servoAzimuth.attach(PINO_SERVO_AZIMUTH);
  servoElevation.attach(PINO_SERVO_ELEVATION);
  servoAzimuth.write(anguloAzimute);
  servoElevation.write(anguloElevacao);
  iniciarConexaoWiFi();
}

void loop() {
  unsigned long tempoAtual = millis();
  verificarBotaoDeModo();

  if (modoLDR) {
    moverServosComLDR();
  } else if (tempoAtual - tempoAnteriorMovimento >= INTERVALO_MOVIMENTO_SERVO) {
    tempoAnteriorMovimento = tempoAtual;
    moverServosSimulado();
  }

  if (tempoAtual - tempoAnteriorLeituras >= INTERVALO_LEITURAS) {
    tempoAnteriorLeituras = tempoAtual;
    lerSensores();
  }

  if (tempoAtual - tempoAnteriorEnvio >= INTERVALO_ENVIO_DADOS) {
    tempoAnteriorEnvio = tempoAtual;
    DadoSolar dadosAtuais = { (int)servoAzimuth.read(), (int)servoElevation.read(), voltagemAtual, correnteAtual };
    if (modoOffline) salvarNaEEPROM(dadosAtuais);
    else enviarDadosThingSpeak(dadosAtuais.azimute, dadosAtuais.elevacao, dadosAtuais.voltagem, dadosAtuais.corrente);
  }

  if (WiFi.status() != WL_CONNECTED) {
    if (!modoOffline) {
      modoOffline = true;
      animacaoErro();
    }
    if (tempoAtual - tempoAnteriorReconexao >= INTERVALO_RECONEXAO) {
      tempoAnteriorReconexao = tempoAtual;
      iniciarConexaoWiFi();
    }
  } else if (modoOffline) {
    enviarDadosEEPROM();
  }
}
`; 

    const block = document.getElementById("codigo");
    block.textContent = codigoArduino;
    Prism.highlightAll();
  </script>

</body>
</html>



